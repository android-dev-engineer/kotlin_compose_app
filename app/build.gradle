plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
}

android {
    def properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())

    namespace 'com.android.dev.engineer.kotlin.compose'
    compileSdk 33

    buildFeatures {
        buildConfig = true // This will be false by default in AGP 8.X
    }

    defaultConfig {
        applicationId application_id
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName version_name
        buildConfigField "String", "API_KEY", "\"${properties.getProperty("api_key")}\""
        // Hilt Test Runner: https://developer.android.com/training/dependency-injection/hilt-testing#instrumented-tests
        testInstrumentationRunner = "com.android.dev.engineer.kotlin.compose.runner.HiltTestRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    // Required for Gradle 8.x updated: : https://docs.gradle.org/current/userguide/upgrading_version_7.html#potential_breaking_changes_2
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_11
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.7'
    }

    testOptions {
        unitTests {
            // Robolectric config: https://robolectric.org/getting-started
            includeAndroidResources = true
        }
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

// Hilt config: https://developer.android.com/training/dependency-injection/hilt-android#setup
kapt {
    correctErrorTypes = true
}

dependencies {
    implementation "androidx.activity:activity-compose:1.7.1"
    implementation "androidx.compose.material:material:1.4.3"
    // Coroutine Android: https://github.com/Kotlin/kotlinx.coroutines#android
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"

    // Navigation https://developer.android.com/jetpack/compose/navigation#setup
    implementation "androidx.navigation:navigation-compose:2.5.3"

    // Splash screen: https://developer.android.com/develop/ui/views/launch/splash-screen/migrate
    implementation "androidx.core:core-splashscreen:1.0.1"

    // Lifecycle scope: https://developer.android.com/topic/libraries/architecture/coroutines#dependencies
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.6.1"

    // Hilt: https://developer.android.com/training/dependency-injection/hilt-android
    implementation "com.google.dagger:hilt-android:2.46.1"
    kapt "com.google.dagger:hilt-compiler:2.46.1"
    // Navigation with Hilt: https://developer.android.com/jetpack/compose/libraries#hilt-navigation
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation "io.coil-kt:coil-compose:2.3.0" // Version 2.4.0 causing coroutine test issues

    // DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Data layer
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.moshi:moshi:1.15.0"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:1.15.0"
    implementation "com.squareup.okhttp3:okhttp:4.11.0"

    // Encrypt data: https://developer.android.com/topic/security/data#include-library
    implementation "androidx.security:security-crypto:1.0.0"

    // Paging: https://developer.android.com/jetpack/androidx/releases/paging#declaring_dependencies
    implementation "androidx.paging:paging-runtime-ktx:3.1.1"
    implementation "androidx.paging:paging-compose:1.0.0-alpha19"

    // Test
    // Coroutines: https://developer.android.com/kotlin/coroutines/test
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    testImplementation "junit:junit:4.13.2"
    testImplementation 'org.robolectric:robolectric:4.10.3'

    // Robolectric Hilt: https://dagger.dev/hilt/robolectric-testing.html
    testImplementation "com.google.dagger:hilt-android-testing:2.46.1"
    testImplementation 'androidx.test:core-ktx:1.5.0'
    testImplementation "com.squareup.okhttp3:mockwebserver:4.11.0"

    // https://developer.android.com/jetpack/compose/testing#setup
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
    androidTestImplementation "com.google.truth:truth:1.1.3"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "com.google.dagger:hilt-android-testing:2.46.1"
}